apply plugin: "groovy"
apply plugin: "idea"

repositories {
    jcenter()
}

sourceSets {
    functional
}

def config = new ConfigSlurper('sys').parse(new File("gradle\\config\\config.groovy").text)

// The drivers we want to use
ext.drivers = ["chrome", "ie"] //TODO: Add IE + Firefox config

dependencies {
    def gebVersion = "0.9.2"
    def seleniumVersion = "2.42.0"

    // If using Spock, need to depend on geb-spock
    functionalCompile "org.gebish:geb-spock:$gebVersion"
    functionalCompile "org.spockframework:spock-core:0.7-groovy-2.0"

    // If using JUnit, need to depend on geb-junit (3 or 4)
    functionalCompile "org.gebish:geb-junit4:$gebVersion"

    // Drivers
    drivers.each { driver ->
        functionalCompile "org.seleniumhq.selenium:selenium-$driver-driver:$seleniumVersion"
    }
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        reports {
            html.destination = reporting.file("$name/tests")
            junitXml.destination = file("$buildDir/test-results/$name")
        }

        systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
        systemProperty "geb.env", driver
        systemProperty "geb.build.baseUrl", new File(config.baseUrl).toURI()

        classpath = sourceSets.functional.runtimeClasspath
        testClassesDir = sourceSets.functional.output.classesDir

        testLogging {
            events 'started', 'passed'
        }
        // If you wanted to set the baseUrl in your buildâ€¦
        // systemProperty "geb.build.baseUrl", "http://myapp.com"
    }
}

task functionalTest {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = false
}